/*
 *  License (BSD Style License):
 *   Copyright (c) 2011
 *   Software Engineering
 *   Department of Computer Science
 *   Technische Universität Darmstadt
 *   All rights reserved.
 * 
 *   Redistribution and use in source and binary forms, with or without
 *   modification, are permitted provided that the following conditions are met:
 * 
 *   - Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   - Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   - Neither the name of the Software Engineering Group or Technische 
 *     Universität Darmstadt nor the names of its contributors may be used to 
 *     endorse or promote products derived from this software without specific 
 *     prior written permission.
 * 
 *   THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 *   AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 *   IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 *   ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 *   LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 *   CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 *   SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 *   INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 *   CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 *   ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 *   POSSIBILITY OF SUCH DAMAGE.
 */
 
/**
 * @author Tanya Harizanova, Robert Cibulla
 *
 *  This file transforms a Vespucci model of version 1 (prior to 2011-11-29) to
 * the newer version 2011-11-29.
 *
 * 
 * --- MODEL part ---
 */

import VespucciConverter.Utils.TransformationHelperLibrary;


modeltype OldModel 'strict' uses 'http://vespucci.editor/2011-06-01';
modeltype NewModel 'strict' uses  'http://vespucci.editor/2011-11-29';

transformation migrate_v1_to_v2(in oldModel : OldModel, out newModel : NewModel);

main() {
	oldModel.rootObjects()[ShapesDiagram]->map toNew();
}

mapping OldModel::ShapesDiagram::toNew() : NewModel::ArchitectureModel {
	self->allSubobjectsOfKind(Shape)->forEach(s){
		s.oclAsType(Shape).targetConnections->forEach(con){
			switch{
				case (con.oclIsTypeOf(Expected)) connections += con.oclAsType(Expected).map toExpected();
				case (con.oclIsTypeOf(InAndOut)) connections += con.oclAsType(InAndOut).map toInAndOut();
				case (con.oclIsTypeOf(Incoming)) connections += con.oclAsType(Incoming).map toIncoming();
				case (con.oclIsTypeOf(NotAllowed)) connections += con.oclAsType(NotAllowed).map toNotAllowed();
				case (con.oclIsTypeOf(Outgoing)) connections += con.oclAsType(Outgoing).map toOutgoing();
				case (con.oclIsTypeOf(Violation)) connections += con.oclAsType(Violation).map toViolation();
				case (con.oclIsTypeOf(GlobalIncoming)) connections += con.oclAsType(GlobalIncoming).map toGlobalIncoming();
				case (con.oclIsTypeOf(GlobalOutgoing)) connections += con.oclAsType(GlobalOutgoing).map toGlobalOutgoing();
			};
		};
	};
	
	ensembles += self.shapes.map toEnsemble();
	ensembles += self.shapes.map toEmpty();

	result.remember();
}


mapping OldModel::Shape::toEnsemble() : NewModel::Ensemble
inherits OldModel::Shape::toShape
when {self.metaClassName() = 'Ensemble'} {
	ensembles += self->oclAsType(OldModel::Ensemble).shapes.map toEnsemble();
	ensembles += self->oclAsType(OldModel::Ensemble).shapes.map toEmpty();
}

mapping OldModel::Shape::toEmpty() : NewModel::Empty
inherits OldModel::Shape::toShape
when {self.metaClassName() = 'Dummy'} {

}




abstract mapping OldModel::Shape::toShape() : NewModel::AbstractEnsemble {	
	name := self.name;
	_query := self._query;
	description := self.description;
	result.remember(self);
	
}


mapping OldModel::Connection::toExpected() : NewModel::Expected
inherits OldModel::Connection::toConnection
when {self.metaClassName() = 'Expected'} {
}

mapping OldModel::Connection::toInAndOut() : NewModel::InAndOut
inherits OldModel::Connection::toConnection
when {self.metaClassName() = 'InAndOut'} {
}

mapping OldModel::Connection::toIncoming() : NewModel::Incoming
inherits OldModel::Connection::toConnection
when {self.metaClassName() = 'Incoming'} {
}

mapping OldModel::Connection::toNotAllowed() : NewModel::NotAllowed
inherits OldModel::Connection::toConnection
when {self.metaClassName() = 'NotAllowed'} {
}

mapping OldModel::Connection::toOutgoing() : NewModel::Outgoing
inherits OldModel::Connection::toConnection
when {self.metaClassName() = 'Outgoing'} {
}

mapping OldModel::Connection::toViolation() : NewModel::Violation
inherits OldModel::Connection::toConnection
when {self.metaClassName() = 'Violation'} {
}

mapping OldModel::Connection::toGlobalIncoming() : NewModel::GlobalIncoming
inherits OldModel::Connection::toConnection
when {self.metaClassName() = 'GlobalIncoming'} {
}

mapping OldModel::Connection::toGlobalOutgoing() : NewModel::GlobalOutgoing
inherits OldModel::Connection::toConnection
when {self.metaClassName() = 'GlobalOutgoing'} {
}

abstract mapping OldModel::Connection::toConnection() : NewModel::Connection {
	name := self.name;
	
	self.originalSource->forEach(os) {
		originalSource += os.late resolveone(NewModel::AbstractEnsemble);
	};
	
	self.originalTarget->forEach(ot) {
		originalTarget += ot.late resolveone(NewModel::AbstractEnsemble);
	};
	
	source := self.source.late resolveone(NewModel::AbstractEnsemble);
	target := self.target.late resolveone(NewModel::AbstractEnsemble);
	
	temp := self.temp;
	
	result.remember(self);
}
